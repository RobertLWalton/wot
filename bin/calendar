#!/bin/sh
#
# calendar printout program
#
# File:		calendar
# Author:	Bob Walton (walton@deas.harvard.edu)
# Date:		Tue Mar  2 13:13:56 EST 2004
#
# The authors have placed this program in the public
# domain; they make no warranty and accept no liability
# for this program.
#
# RCS Info (may not be true date or author):
#
#   $Author: walton $
#   $Date: 2004/03/02 18:31:10 $
#   $RCSfile: calendar,v $
#   $Revision: 1.5 $
#
# The next line starts tclsh \
exec tclsh "$0" "$@"

set document "
calendar \[date\] <calendar-file

Outputs each line of calendar-file that contains a date
or any of the following three days.  The date argument
defaults to the current date.  Dates in the file are in
`month day' form, where month can be a number or an ab-
breviation with 3 or more letters.  The month and day
can be separated by any sequence of spaces, commas, and
slashes.  Years are ignored."

if { [regexp {^-doc} [lindex $argv 0]] } {
    puts $document
    exit 1
}

set mark "************************************"
set mark $mark$mark

# set month = {m1 m2 m3 m4} and day = {d1 d2 d3 d4}
# where m1/d1 is today, m2/d2 tommorrow, m3/d3 the
# day after tommorrow, etc.
#
if { $argc >= 1 } {
    set time [clock scan [lindex $argv 0]]
} else {
    set time [clock seconds]
}
set month {}
set day {}
set count 1
while { $count <= 4 } {
    set date [clock format $time -format "%m %d"]
    set m [lindex $date 0]
    regexp {^0*([1-9].*)$} $m forget m
    set d [lindex $date 1]
    regexp {^0*([1-9].*)$} $d forget d
    lappend month $m
    lappend day $d
    incr time [expr 24 * 60 * 60]
    incr count
}

# Go through the lines of stdin printing those with
# a date matching one of those listed in month/day.
#
set found no
while { "yes" } {

    set line [gets stdin]
    if { [eof stdin] } break
    regsub -all "\[ \t,/\]+" $line " " items
    set last ""
    foreach item [split $items " "] {
	regexp {^0*([1-9].*)$} $item forget item
	set index [lsearch -exact $day $item]
        if { $index < 0 } {
	    set last $item
	    continue
	}

	set last [string tolower $last]
	set len [string length $last]
	set lenm1 [expr $len - 1]
	if {    [regexp {^[a-z]*$} $last] \
	     && $len >= 3 } {
	    set m 1
	    foreach mon { january february march \
	    		  april may june \
			  july august sepember \
			  october november december \
			} {
	        if {    [string range $mon 0 $lenm1] \
		     == $last } {
		    set last $m
		    break
		}
		incr m
	    }
	}

	regexp {^0*([1-9].*)$} $last forget last
        if { $last == [lindex $month $index] } {
	    if { $found == "no" } {
	        set found yes
		puts ""
		puts $mark
	    }
	    puts $line
	}
	set last $item
    }
}
if { $found } {
    puts $mark
    puts ""
}
exit 0
