#! /bin/csh -fe
#
# Make a copy of a CVS ,v repository file.
#
# File:		cvscp
# Author:	Bob Walton <walton@acm.org>
# Date:		Fri Jul 11 11:13:54 EDT 2008
#
# The authors have placed this program in the public
# domain; they make no warranty and accept no liability
# for this program.
#
# RCS Info (may not be true date or author):
#
#   $Author: walton $
#   $Date: 2008/07/11 15:28:15 $
#   $RCSfile: cvscp,v $
#   $Revision: 1.3 $

switch ( "$1" )

   case -doc*:

   	echo "\
cvscp file1 file2\
\
    First cvs commit file1.  Then copy the CVS\
    repository file file1,v to the CVS repository file\
    file2,v, and then cvs update file2 in the current\
    directory, effectively copying file1 to file2 in\
    the current directory.  End by setting the user\
    write mode bit on file2.\
"
    	exit 1
	breaksw
endsw

echo cvs commit -m update "$1"
cvs commit -m update "$1"
set root=`cat CVS/Root`
set account=`expr "$root" : '\([^:]*\):' `
set root=`expr "$root" : '[^:]*:\(.*\)$' `
set repository="$root/`cat CVS/Repository`"
echo ssh "$account" cp -p "$repository/$1,v" "$repository/$2,v"
ssh "$account" cp -p "$repository/$1,v" "$repository/$2,v"
echo cvs update "$2"
cvs update "$2"
echo chmod u+w "$2"
chmod u+w "$2"
